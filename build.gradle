/*
 * Copyright 2000-2022 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.github.jk1.license.render.JsonReportRenderer

plugins {
  id "com.github.jk1.dependency-license-report" version "1.17"
}

ext {
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2021.2'
  versionNumber = project.hasProperty('PluginVersion') ? property('PluginVersion') : teamcityVersion + "-SNAPSHOT"

  teamCityDir = project.getProperties()["path.variable.teamcitydistribution"]

  teamcityLibs = project.hasProperty("TeamCityLibs") ? "$TeamCityLibs" : teamCityDir + "/webapps/ROOT/WEB-INF/lib"
  teamcityTestLibs = project.hasProperty("TeamCityTestLibs") ? "$TeamCityTestLibs" : teamCityDir + "/devPackage/tests"
  teamcityAgentLibs = project.hasProperty("TeamCityAgentLibs") ? "$TeamCityTestLibs" : teamCityDir + "/buildAgent/lib"
}

group = 'teamcity-gradle-plugin'
version = versionNumber


allprojects {
  group = 'teamcity-gradle-plugin'
  version = versionNumber

  repositories {
    mavenLocal()
    maven {
      name = "jetbrains-teamcity"
      url = "https://download.jetbrains.com/teamcity-repository"
    }
  }
}

subprojects {
  apply plugin: "java"

  java {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
  }

  test.useTestNG()
  jar.archiveVersion.set('')
}

licenseReport {
  renderers = [ new JsonReportRenderer('third-party-libs.json') ]
}
